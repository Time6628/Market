plugins {
    id "org.spongepowered.plugin" version "0.8.1"
    id "com.qixalite.spongestart" version "1.6.0"
    id "com.github.johnrengelman.shadow" version "1.2.3"
    id "ninja.miserable.blossom" version "1.0.1"
    id "java"
    id "idea"
}

group = pluginGroup
version = pluginVersion
if (System.getenv().BUILD_NUMBER != null) {
    version += ".${System.getenv().BUILD_NUMBER}"
} else {
    version += ".0"
}

repositories {
    mavenCentral()
    jcenter()
    maven {
        name "sonatype"
        url "http://oss.sonatype.org/content/repositories/snapshots/"
    }
    maven {
        name "jitpack.io"
        url "https://jitpack.io"
    }
}
dependencies {
    compile "org.spongepowered:spongeapi:7.+"
    implementation 'com.github.codeHusky:HuskyUI-Plugin:+'
    shadow "redis.clients:jedis:2.9.0"
    shadow "org.apache.commons:commons-pool2:2.4.2"
    shadow "org.mongodb:mongodb-driver:3.5.0"
    shadow "com.amazonaws:aws-java-sdk-dynamodb:1.11.202"
}

configurations {
    compile.extendsFrom shadow
}

sponge.plugin.id = pluginId

blossom {
    def mainClass = '/src/main/java/com/kookykraftmc/market/Market.java'

    replaceToken '@VERSION@', version, mainClass
}

shadowJar {

    configurations = [project.configurations.shadow]
    dependencies {
        include dependency("redis.clients:jedis")
        include dependency("org.apache.commons:commons-pool2")
        include dependency("org.mongodb:mongodb-driver")
        include dependency("org.mongodb:mongodb-driver-core")
        include dependency("org.mongodb:bson")
        include dependency("com.amazonaws:aws-java-sdk-dynamodb")
        include dependency("com.amazonaws:aws-java-sdk-core")
        include dependency("com.amazonaws:aws-java-sdk-kms")
        include dependency("com.amazonaws:aws-java-sdk-s3")
        include dependency("com.amazonaws:jmespath-java")
        include dependency("commons-logging:commons-logging")
    }

    relocate "org.apache", "com.kookykraftmc.relocate.org.apache"
    relocate "redis.clients", "com.kookykraftmc.relocate.redis.clients"
    relocate "com.mongodb", "com.kookykraftmc.relocate.com.mongodb"
    relocate "org.bson", "com.kookykraftmc.relocate.org.bson"
    relocate "com.amazonaws", "com.kookykraftmc.relocate.com.amazonaws"
    relocate "org.apache.commons.logging", "com.kookykraftmc.reloate.org.apache"

    archiveName = "Market-" + version + ".jar"
}

build.dependsOn(shadowJar)
jar.enabled = false